  Функция ТяжелаяОперация(Период)    Экспорт
	  
	Прогресс = 0; 
	ДлительныеОперации.СообщитьПрогресс(Прогресс);

	Запрос = Новый Запрос;
	Запрос.Текст = 
					"ВЫБРАТЬ
			|	ВКМ_ВыполненныеКлиентуРаботы.Договор КАК Договор,
			|	СУММА(ВКМ_ВыполненныеКлиентуРаботы.КоличествоЧасов) КАК Количество,
			|	СУММА(ВКМ_ВыполненныеКлиентуРаботы.Сумма) КАК Сумма,
			|	ВКМ_НоменклатураРаботыСпециалиста.Значение КАК Значение
			|ПОМЕСТИТЬ ВТ_Запрос
			|ИЗ
			|	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы КАК ВКМ_ВыполненныеКлиентуРаботы,
			|	Константа.ВКМ_НоменклатураРаботыСпециалиста КАК ВКМ_НоменклатураРаботыСпециалиста
			|ГДЕ
			|	ВКМ_ВыполненныеКлиентуРаботы.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
			|	И ВКМ_ВыполненныеКлиентуРаботы.ОписаниеРабот <> ""Абонплата""
			|	И ВКМ_ВыполненныеКлиентуРаботы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|
			|СГРУППИРОВАТЬ ПО
			|	ВКМ_ВыполненныеКлиентуРаботы.Договор,
			|	ВКМ_НоменклатураРаботыСпециалиста.Значение
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	ДоговорыКонтрагентов.Ссылка,
			|	1,
			|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты,
			|	ВКМ_НоменклатураАбонентскаяПлата.Значение
			|ИЗ
			|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов,
			|	Константа.ВКМ_НоменклатураАбонентскаяПлата КАК ВКМ_НоменклатураАбонентскаяПлата
			|ГДЕ
			|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора					;
			|					
			|					////////////////////////////////////////////////////////////////////////////////
			|					ВЫБРАТЬ
			|						ВТ_Запрос.Договор.Организация КАК Организация,
			|						ВТ_Запрос.Договор.Владелец КАК Контрагент,
			|						ВТ_Запрос.Договор КАК Договор,
			|						ВТ_Запрос.Договор КАК Основание,
			|						ВТ_Запрос.Значение КАК Номенклатура,
			|						ВТ_Запрос.Количество КАК Количество,
			|						ВТ_Запрос.Сумма / ВТ_Запрос.Количество КАК Цена,
			|						ВТ_Запрос.Сумма КАК Сумма
			|					ИЗ
			|						ВТ_Запрос КАК ВТ_Запрос
			|					ГДЕ
			|						ВТ_Запрос.Сумма > 0
			|					
			|					УПОРЯДОЧИТЬ ПО
			|						Организация,
			|						Контрагент,
			|						Договор
			|					ИТОГИ
			|						СУММА(Сумма)
			|					ПО
			|						Договор";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ОкончаниеПериода", Период.ДатаОкончания);
	ВыборкаДокумент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Индекс = 0;                                  
	Всего = ВыборкаДокумент.Количество();
	Пока ВыборкаДокумент.Следующий() Цикл
		Если ЕстьАкты(ВыборкаДокумент, Период) Тогда
			Продолжить;
		КонецЕсли;
		НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ВыборкаДокумент);            
		НовыйДокумент.Дата = Период.ДатаОкончания;   
		НовыйДокумент.Комментарий = "Создана при Массовом создании актов";
		ВыборкаТЧ = ВыборкаДокумент.Выбрать();
		Пока ВыборкаТЧ.Следующий() Цикл
			НоваяСтрокаТЧ = НовыйДокумент.Услуги.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, ВыборкаТЧ);
		КонецЦикла;
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);

		// {{Мымрин С. В.
		// Следующий код для замедления процесса создания документов  
		
		//КонецВыполнения = ТекущаяДатаСеанса() + 2;
		//Пока ТекущаяДатаСеанса() < КонецВыполнения Цикл
		//КонецЦикла;
		
		//Мымрин С. В.}}
		
		Индекс = Индекс + 1;
		Прогресс = Индекс / Всего * 100;
		ДлительныеОперации.СообщитьПрогресс(Прогресс);
	КонецЦикла;
	
	Возврат Индекс;
	
КонецФункции   

Функция ЕстьАкты(Документ, Период) 
	
	ОниЕсть = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
		|	И РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И РеализацияТоваровУслуг.Организация = &Организация
		|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
		|	И РеализацияТоваровУслуг.Договор = &Договор
		|	И РеализацияТоваровУслуг.СуммаДокумента = &СуммаДокумента";
	
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания +1);
	Запрос.УстановитьПараметр("Договор", Документ.Договор);
	Запрос.УстановитьПараметр("Контрагент", Документ.Контрагент);
	Запрос.УстановитьПараметр("Организация", Документ.Организация);
	Запрос.УстановитьПараметр("СуммаДокумента", Документ.Сумма);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ОниЕсть = Истина;
	КонецЕсли;
	
	Возврат ОниЕсть;
	
КонецФункции  