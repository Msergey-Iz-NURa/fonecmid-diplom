
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОУ";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	КомандаПечати.Порядок = 5;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОУ");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАкта(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_АктОказанныхУслуг";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Акт";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_АктОказанныхУслуг");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);   
	
//	ДанныеДокументов1 = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивОбъектов,"Ссылка,Номер,Дата,Организация,Организация,Контрагент,Договор,СуммаДокумента");
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("ПредставлениеПолучателя", ДанныеДокументов.Контрагент);
		
		ШаблонЗаголовка = "Акт об оказанных услугах № %1 от %2";
		ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
			Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ОбластьЗаголовок.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);

		ОбластьРеквизитыСторон = Макет.ПолучитьОбласть("РеквизитыСторон");
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("ПредставлениеИсполнителя", "" + ДанныеДокументов.Организация + ДанныеДокументов.ОрганизацияИНН + ДанныеДокументов.ОрганизацияКПП); 
		ДанныеПечати.Вставить("ПредставлениеЗаказчика", "" + ДанныеДокументов.Контрагент + ДанныеДокументов.КонтрагентИНН + ДанныеДокументов.КонтрагентКПП); 
		ДанныеПечати.Вставить("Основание", ДанныеДокументов.Договор);
		ОбластьРеквизитыСторон.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьРеквизитыСторон);
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);  
		
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
		Пока ВыборкаУслуги.Следующий() Цикл
			ОбластьСтрока.Параметры.Заполнить(ВыборкаУслуги);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
		КонецЦикла;

		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
  		ОбластьИтого = Макет.ПолучитьОбласть("Итого");
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("Всего", ДанныеДокументов.СуммаДокумента);
		ОбластьИтого.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьИтого);

		ОбластьСуммаПрописью = Макет.ПолучитьОбласть("СуммаПрописью");
		ДанныеПечати = Новый Структура;     
		ИтоговаяСтрока = СтрШаблон("Всего оказано услуг %1, на сумму %2 руб.",
			ДанныеДокументов.Услуги.Выбрать().Количество(), ДанныеДокументов.СуммаДокумента);	
		ДанныеПечати.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);  
		ФормСтрока = "Л = ru_RU; ДП = Ложь";   
		ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2";
		ДанныеПечати.Вставить("СуммаПрописью", ЧислоПрописью(ДанныеДокументов.СуммаДокумента, ФормСтрока, ПарПредмета));
		ОбластьСуммаПрописью.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
  		ОбластьПодвалАкта = Макет.ПолучитьОбласть("ПодвалАкта");
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("ПредставлениеИсполнителя", ДанныеДокументов.Организация);
		ДанныеПечати.Вставить("ПредставлениеЗаказчика", ДанныеДокументов.Контрагент);
		ОбластьПодвалАкта.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьПодвалАкта);

		
		
		
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
	                                            
КонецФункции


Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	               |	РеализацияТоваровУслуг.Номер КАК Номер,
	               |	РеализацияТоваровУслуг.Дата КАК Дата,
	               |	РеализацияТоваровУслуг.Организация КАК Организация,
	               |	ВЫБОР
	               |		КОГДА РеализацияТоваровУслуг.Организация.ИНН = """"
	               |			ТОГДА """"
	               |		ИНАЧЕ "", ИНН "" + РеализацияТоваровУслуг.Организация.ИНН
	               |	КОНЕЦ КАК ОрганизацияИНН,
	               |	ВЫБОР
	               |		КОГДА РеализацияТоваровУслуг.Организация.КПП = """"
	               |			ТОГДА """"
	               |		ИНАЧЕ "", КПП "" + РеализацияТоваровУслуг.Организация.КПП
	               |	КОНЕЦ КАК ОрганизацияКПП,
	               |	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	               |	ВЫБОР
	               |		КОГДА РеализацияТоваровУслуг.Контрагент.ИНН = """"
	               |			ТОГДА """"
	               |		ИНАЧЕ "", ИНН "" + РеализацияТоваровУслуг.Контрагент.ИНН
	               |	КОНЕЦ КАК КонтрагентИНН,
	               |	ВЫБОР
	               |		КОГДА РеализацияТоваровУслуг.Контрагент.КПП = """"
	               |			ТОГДА """"
	               |		ИНАЧЕ "", КПП "" + РеализацияТоваровУслуг.Контрагент.КПП
	               |	КОНЕЦ КАК КонтрагентКПП,
	               |	РеализацияТоваровУслуг.Договор КАК Договор,
	               |	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваровУслуг.Ответственный КАК Ответственный,
	               |	РеализацияТоваровУслуг.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции




#КонецОбласти  

#КонецЕсли